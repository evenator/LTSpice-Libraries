* Copyright © Linear Technology Corp. 01/09/2007. All rights reserved.
*
* Note 1: For greater numerical accuracy in transient simulation, decrease  
* the maximum simulation time step.
*
* Note 2: This is a simplified model with no shutdown functionality.
*
* Node List (same as IC): GND IN1 IN2 VSS VOUT OUTRTN VDD
.SUBCKT LTC1966 1 2 3 4 5 6 7
ISUP1 7 1 1.55E-4
ISUP2 1 4 1.2E-5
GIN2 0 11 VALUE={1m*ABS(V(10)/(V(5,6)+4E-4)*MIN(V(5,6)*4.183,ABS(V(10))))}
RIN1 10 0 1E3
RIN2 11 13 3E5
DS1 13 0 DS
CIN1 10 0 1.6E-10
DS3 12 31 DS
RIN3 12 11 50
EOUT 20 0 VALUE={ABS(V(11))+V(6)}
ROUT 5 20 8.5E4
COUT 5 6 8.7E-12
GIN1 0 10 22 33 1E-3
RIN4 11 0 1E3
EVCC 31 0 VALUE={0.56*V(7,1)-0.750}
CIN2 11 0 1E-11
CS2 33 1 2.5E-12
CS1 22 1 2.5E-12
DIN1 2 7 DIN
DIN2 4 2 DIN
DIN3 4 3 DIN
DIN4 3 7 DIN
RS1 22 2 6E3
RS2 33 3 6E3
RS3 22 33 1E8
.MODEL DS D (IS=1E-16 RS=0 KF=0 XTI=0)
.MODEL DIN D (IS=1E-13 RS=0 KF=0 XTI=0)
.ENDS LTC1966new
