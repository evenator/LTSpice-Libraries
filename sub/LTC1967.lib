* Copyright © Linear Technology Corp. 01/09/2007. All rights reserved.
*
* Note 1: For greater numerical accuracy in transient simulation, decrease  * the maximum simulation time step.
*
* Note 2: This is a simplified model with no shutdown functionality.
*
* Node List (same as IC): GND IN1 IN2 VOUT OUTRTN V+
.SUBCKT LTC1967 1 2 3 5 6 7
ISUP1 7 1 3.3E-4
GIN2 0 11 VALUE={1m*ABS(V(10)/(V(5,6)+0.002)*MIN(V(5,6)*4.183+1E-6,ABS(V(10))))}
RIN1 10 0 1E3
RIN2 11 13 1.5E5
DS1 13 0 DS
CIN1 10 0 4E-11
DS3 12 31 DS
RIN3 12 11 50
EOUT 20 0 VALUE={ABS(V(11))+0.002+V(6)}
ROUT 5 20 5E4
COUT 5 6 8.7E-12
GIN1 0 10 22 33 0.001
RIN4 11 0 1E3
EVCC 31 0 VALUE={0.5*V(7,1)-0.750}
CIN2 11 0 1E-11
CS1 33 1 8E-13
CS2 22 1 8E-13
DIN1 2 7 DIN
DIN2 4 2 DIN
DIN3 4 3 DIN
DIN4 3 7 DIN
RS1 22 2 2E3
RS2 33 3 2E3
RS3 22 33 1E8
.MODEL DIN D (IS=1E-13 RS=0 KF=0 XTI=0)
.MODEL DS D (IS=1E-16 RS=0 KF=0 XTI=0)
.ENDS LTC1967
